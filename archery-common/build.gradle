dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury:${rootProject.architectury_version}"

    modApi("maven.modrinth:modular-item-api:${rootProject.miapi_version}-${rootProject.minecraft_version}-fabric")
    implementation('org.mariuszgromada.math:MathParser.org-mXparser:4.4.2')
    modApi("com.github.RedPxnda.Nucleus:nucleus:${rootProject.nucleus_version}")
}

repositories {
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://www.cursemaven.com' }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
}

architectury {
    common('fabric', 'forge')
}

tasks.register('githubPublish') {
    finalizedBy ':archery-common:publish'
    group = 'publishing'

    doLast {
        publishing.publications.mavenCommon.groupId = "com.${rootProject.github}"
    }
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            version = rootProject.mod_version + "-" + rootProject.minecraft_version

            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven { url uri(file("$rootDir/build/repository")) }
    }
}