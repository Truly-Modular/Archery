plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
    id "io.github.pacifistmc.forgix" version "1.2.6"
}

architectury {
    minecraft = rootProject.minecraft_version
}

repositories {
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://www.cursemaven.com' }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
}

forgix {
    group = "smartin.miapi"
    mergedJarName = "Truly-Modular-Archery-${rootProject.mod_version}-${rootProject.minecraft_version}.jar"
}

tasks.register('clean-builds') {
    dependsOn(':archery-forge:clean',':archery-fabric:clean')
    doLast {
        println "Cleaned previous build"
    }
}

tasks.register('buildBoth') {
    dependsOn('clean-builds', ':archery-fabric:build', ':archery-forge:build')
    doLast {
        def modVersion = rootProject.mod_version
        def minecraftVersion = rootProject.minecraft_version
        def fabricBuildDir = project(':archery-fabric').file('build/libs')
        def forgeBuildDir = project(':archery-forge').file('build/libs')
        def fabricJar = fabricBuildDir.listFiles().find { it.name.endsWith("${modVersion}.jar") }
        def forgeJar = forgeBuildDir.listFiles().find { it.name.endsWith("${modVersion}.jar") }

        if (fabricJar) {
            copy {
                from fabricJar
                into project.file('Merged')
                rename { fileName ->
                    "Truly-Modular-Archery-${modVersion}-${minecraftVersion}-snapshot-fabric.jar"
                }
            }
        }

        if (forgeJar) {
            copy {
                from forgeJar
                into project.file('Merged')
                rename { fileName ->
                    "Truly-Modular-Archery-${modVersion}-${minecraftVersion}-snapshot-forge.jar"
                }
            }
        }

        println "Builds completed"
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:@YARN_MAPPINGS@:v2"
    }
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    processResources {
        def expandProps = [
                "version"                   : rootProject.mod_version,
                "group"                     : rootProject.group, //Else we target the task's group.
                "minecraft_version"         : rootProject.minecraft_version,
                "architectury_version"      : rootProject.architectury_version,
                "github"                    : rootProject.github,
                "miapi_version"             : rootProject.miapi_version,
                "forge_version"             : rootProject.forge_version,
                "forge_loader_version_range": rootProject.forge_loader_version_range,
                "forge_version_range"       : rootProject.forge_version_range,
                "minecraft_version_range"   : rootProject.minecraft_version,
                "fabric_api_version"        : rootProject.fabric_api_version,
                "fabric_loader_version"     : rootProject.fabric_loader_version,
                "mod_name"                  : rootProject.mod_name,
                "mod_author"                : rootProject.mod_author,
                "mod_id"                    : rootProject.mod_id,
                "mod_description"           : rootProject.mod_description,
                "credits"                   : rootProject.credits
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
